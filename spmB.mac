! units: mm, ton, s; MPa, mm/s2, N
! cs: beam points to x, up is z
finish
/clear, nostart



! NODE NUMBERS
!          4    5   
!          14 15  
!       3         6                             
!        13     16                
!    2              7             
!     12             8          
! 1                         
!11                          
!   19                       
!   (21,22,23,26,92) equivalent on other side where applicable               

alpha = arg1 ! luffing [deg]
beta = arg2 ! jibbing [deg]
liveload = arg3 ! load in tip [ton]

:module
*afun, rad
pi = acos(-1) 
*afun, deg
Emodulus = 210000 ! MPa
gravity = 9806    ! mm/s/s

acelx = 0.11
acely = 0.19
acelz = 0.12


! Members: lenghts sort of arbitrary
blfp = 3*5800+500 !boom tip from pivot
jrtt = 3*5800+500-3500  ! jib rotationpoint to tip

! Mechanism: Following parameters taken from CAD drawing
btta = 6217 ! boom tip to actuator
bctr = 700 ! boom centerlinte to rotatejib
bcta = 700+250 ! boom centerline to actuator
jrax = 2046.4*cos(34.5) ! Jib Rotationpoint to Actuator dX
jray = 2046.4*sin(34.5)! Jib Rotationpoint to Actuator dY

! Actuators: Choosen out of CAD drawing and excel
brod = 160 ! boom actuator rod diametert
bpush = -1000e3 ! push and pull capacities
bpull =   500e3 ! push and pull capacities
jrod = 250 ! jib actuator rod diameter
jpush = -2138e3 ! push and pull capacities
jpull =  1107e3 ! push and pull capacities
brod = jrod   ! the old actuator doesn't suffice in the buckling department
bpush = jpush ! the old actuator doesn't suffice in the buckling department
bpull = jpull ! the old actuator doesn't suffice in the buckling department



/prep7
mptemp,1,0 ! steel in [mm;ton;N]
mpdata,dens,1,,1e-20 !7860e-12 
mpdata,ex,1,,210000
mpdata,prxy,1,,0.3
mpdata,dens,2,,1e-20 !7860e-12 
mpdata,ex,2,,210000
mpdata,prxy,2,,0.3
mpdata,dens,3,,1e-20 ! nihilitium
mpdata,ex,3,,210000e6
mpdata,prxy,3,,0.3
et,1,beam188
sectype,   1, beam, hrec, , 0   
secoffset, cent 
secdata,1000,500,10,10,12,12 
sectype,   3, beam, ctube, , 0   
secoffset, cent 
secdata,160,200,12  ! ri ro n
et,2,link180
r,2,pi/4*jrod**2,0   !( elem, area, mass/length)
r,4,pi/4*brod**2,0   !( elem, area, mass/length)
et,3,mass21
r,3,2.5,2.5,2.5     ! deadload 2.5 tons each * 7 = 17.5 (calculated = 16)
r,5,liveload,liveload,liveload     ! liveload 


n, 1,0,0,0
n,11,0, 1202,0
n,21,0,-1202,0
n, 2,5800,0,0
n,12,5800, 1202,-1450
n,22,5800,-1202,-1450
n, 3,blfp-btta,0,0
n,13,blfp-btta, 500,-bcta
n,23,blfp-btta,-500,-bcta
n, 4,blfp,0,0
n,14,blfp, 500,-bctr
n,24,blfp,-500,-bctr
wpcsys
nwplan,,1
wprota,,,-alpha
transfer,4,0,all
n,19,1500, 1202,-2500
n,29,1500,-1202,-2500
mat,1
type,1
secnum,1
e,1,2 
e,2,3 
e,3,4 
mat,3
secnum,3
e,1,11
e,1,21
e,2,12
e,2,22
e,3,13
e,3,23
e,4,14
e,4,24

cm,boom,elem

nsel,none
n, 5,,0,0
n,15,,500,-bctr
n,25,,-500,-bctr
n, 6,jrax,   0,    0
n,16,jrax, 500,-bctr+jray
n,26,jrax,-500,-bctr+jray
n, 7,jrtt,0,1000
n, 8,jrtt+1000,0,500
wpcsys
wpoffs,,,bctr
transfer,4,0,all
wpcsys
nsel,a,,,4
nwplan,,4
nsel,u,,,4
wprota,,,-alpha
csys,4
wpoffs,,,-bctr
csys,0
wprota,,,beta
transfer,4,0,all
mat,1
secnum,1
e,5,6 
e,6,7 
e,7,8 
mat,3
secnum,3
e,6,16 
e,6,26 
e,5,15
e,5,25
cmsel,u,boom
cm,jib,elem

n,19,1500,1202,-2500
n,29,1500,-1202,-2500
allsel


mat,2
type,2
real,4
e,19,12
e,29,22
real,2
e,13,16
e,23,26
cmsel,u,boom
cmsel,u,jib
cm,actuators,elem
allsel

esel,none
type,3
real,3
e,(1:7:1)
cm,deadload,elem

esel,none
real,5
e,8
cm,liveload,elem
allsel

/solu
d,11,ux,0,,,,uz
d,21,ux,0,,,,uy,uz
d,19,ux,0,,,,uy,uz
d,29,ux,0,,,,uy,uz

cp,next,ux,14,15
cp,next,uz,14,15
cp,next,uy,24,25
cp,next,ux,24,25
cp,next,uz,24,25

acel,(0-acelx)*gravity,(0-acely)*gravity,(1+acelz)*gravity
lswrite,1
acel,(0+acelx)*gravity,(0-acely)*gravity,(1+acelz)*gravity
lswrite,2

lssolve,1,2,1

