! units: mm, ton, s; MPa, mm/s2, N
! cs: beam points to x, up is z
!
! Many parameters are hardcoded, because this is 
! based on an existing design: BAILEY / IHC GUSTO 
! ENGINEERING / TRAVELING OFFSHORE CRANE 3t x30m / 
! 10t x 25m FPSO MYSTRAS V
! 

finish
/clear, nostart
/view,,,-1,

! NODE NUMBERS
!          4    5   
!          14 15  
!       3         6                             
!        13     16                
!    2              7             
!     12             8          
! 1                         
!11                          
!   19                       
!   (21,22,23,24,25,26,29) equivalent on other side where applicable               

alpha = arg1 ! luffing [deg]
beta = arg2 ! jibbing [deg] (180 is stretched, the construction angle)
liveload = arg3 ! load in tip [ton]

:module ! the load case runner steps in here, omiting the /clear and arguments
*afun, rad
pi = acos(-1) 
*afun, deg
Emodulus = 210000 ! MPa
gravity = 9806    ! mm/s/s

! equivelent specific gravity to wind load
windspeed = 36e3        ! mm/s
totalmass = 20          ! t
totalarea = 33e6        ! mm2
rho       = 1.226e-12   ! t / mm3
q = 0.5*windspeed**2*0.75*rho
aw = q*totalarea/totalmass/gravity

! specific gravities
acelx = 0.11+sin(4.3)+aw ! surge, heel, wind
acely = 0.19+sin(2.2)+aw ! sway, trim, wind
acelz = 0.12             ! heave
acelx = 0.19+sin(4.3)+aw ! worst
acely = 0.19+sin(4.3)+aw ! rookworst

! Members: lenghts sort of arbitrary
blfp = 3*5800+500-112+1000  ! boom tip from pivot
jrtt = 3*5800+500-3500+112  ! jib rotationpoint to tip
jahw = 760                  ! jib actuator half width (the length between the two)
jphw = 650                  ! jib pivot half width
bahw = 1202                 ! boom actuator half width
bphw = 1202                 ! boom pivot half width

! Actuators will take up equal force though!
jahw = 1e-17
bahw = 1e-17

! Mechanism: Following parameters taken from CAD drawing
btta = 6199 ! boom tip to actuator
bctr = 730 ! boom centerlinte to rotatejib
bcta = 730+537 ! boom centerline to actuator
jrax = 2046.489*cos(37.152) ! Jib Rotationpoint to Actuator dX
jray = 2046.489*sin(37.152) ! Jib Rotationpoint to Actuator dY


! Actuators: Choosen out of CAD drawing and excel
! Original, estimated from drawing
brod = 160 ! boom actuator rod diametert
bpush = -1000e3 ! push and pull capacities
bpull =   500e3 ! push and pull capacities
brod = 220
bpush = -1689e3
bpull = 891e3
jrod = 250 ! jib actuator rod diameter
jpush = -2138e3 ! push and pull capacities
jpull =  1107e3 ! push and pull capacities



/prep7
mptemp,1,0 ! steel in [mm;ton;N]
mpdata,dens,1,,7860e-12 
mpdata,ex,1,,210000
mpdata,prxy,1,,0.3
mpdata,dens,2,,1e-20 !7860e-12 
mpdata,ex,2,,210000
mpdata,prxy,2,,0.3
mpdata,dens,3,,1e-20 ! nihilitium
mpdata,ex,3,,210000e6
mpdata,prxy,3,,0.3
et,1,beam188
sectype,   1, beam, hrec, , 0   
secoffset, cent 
secdata,1000,500,10,10,12,12 
sectype,   3, beam, ctube, , 0   
secoffset, cent 
secdata,160,200,12  ! ri ro n

sectype,11,beam,hrec
secdata,2404,500,10,10,12,12
sectype,12,beam,hrec
secdata,2404,1500,10,10,12,12
sectype,13,beam,hrec
secdata,450,250,10,10,12,12
sectype,14,taper 
secdata,11,0,0,0
secdata,12,cos(alpha)*(750+5730),0,sin(alpha)*(750+5730)
sectype,15,taper
secdata,12,cos(alpha)*(750+5730),0,sin(alpha)*(750+5730)
secdata,13,cos(alpha)*(750+5730+22670),0,sin(alpha)*(750+5730+22670)

et,2,link180
r,2,pi/4*jrod**2,0   !( elem, area, mass/length)
r,4,pi/4*brod**2,0   !( elem, area, mass/length)
et,3,mass21
r,3,1,1,1    ! deadload 1 tons each * 7 = 7, brings total mass to 22 (calculated = 16)
r,5,liveload,liveload,liveload     ! liveload 


n, 1,0,0,0
n,11,0, bphw,0
n,21,0,-bphw,0
n, 2,750+5730,0,0
n,12,5800, bahw,-1450
n,22,5800,-bahw,-1450
n, 3,blfp-btta,0,0
n,13,blfp-btta, jahw,-bcta
n,23,blfp-btta,-jahw,-bcta
n, 4,blfp,0,0
n,14,blfp, jphw,-bctr
n,24,blfp,-jphw,-bctr
wpcsys
nwplan,,1
wprota,,,-alpha
transfer,4,0,all
mat,1
type,1
secnum,14
e,1,2 
secnum,15
e,2,3 
e,3,4 
mat,3
secnum,3
e,1,11
e,1,21
e,2,12
e,2,22
e,3,13
e,3,23
e,4,14
e,4,24

cm,boom,elem

nsel,none
n, 5,0,0,0
n,15,0, jphw,-bctr
n,25,0,-jphw,-bctr
n, 6,jrax,   0,    0
n,16,jrax, jahw,-bctr+jray
n,26,jrax,-jahw,-bctr+jray
n, 7,jrtt,0,0
n, 8,jrtt+1000,0,500
wpcsys
wpoffs,,,bctr
transfer,4,0,all
wpcsys
nsel,a,,,4
nwplan,,4
nsel,u,,,4
wprota,,,-alpha
csys,4
wpoffs,,,-bctr
csys,0
wprota,,,180-beta ! 180 is stretched, the construction angle
transfer,4,0,all
mat,1


sd1 = nx(7)+(nx(5)-nx(7))/jrtt*22670
sd2 = ny(7)+(ny(5)-ny(7))/jrtt*22670
sd3 = nz(7)+(nz(5)-nz(7))/jrtt*22670
sectype,16,taper
secdata,12,sd1, sd2, sd3
secdata,13,nx(7),ny(7),nz(7)


secnum,16
e,5,6 
e,6,7 
secnum,13
e,7,8 
mat,3
secnum,3
e,6,16 
e,6,26 
e,5,15
e,5,25
cmsel,u,boom
cm,jib,elem

n,19,1300, bahw,-2500
n,29,1300,-bahw,-2500
allsel


mat,2
type,2
real,4
e,19,12
e,29,22
real,2
e,13,16
e,23,26
cmsel,u,boom
cmsel,u,jib
cm,actuators,elem
allsel

esel,none
type,3
real,3
e,(1:7:1)
cm,deadload,elem

esel,none
real,5
e,8
cm,liveload,elem
allsel

/solu
d,11,ux,0,,,,uz
d,21,ux,0,,,,uy,uz
d,19,ux,0,,,,uy,uz
d,29,ux,0,,,,uy,uz

cp,next,ux,14,15
cp,next,uz,14,15
cp,next,uy,24,25
cp,next,ux,24,25
cp,next,uz,24,25

acel,(0-acelx)*gravity,(0-acely)*gravity,(1+acelz)*gravity
lswrite,1
acel,(0+acelx)*gravity,(0-acely)*gravity,(1+acelz)*gravity
lswrite,2

lssolve,1,2,1

! /esha,1 
! gplo
! /rep 
! /eof
